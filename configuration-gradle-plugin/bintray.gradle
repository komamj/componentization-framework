apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

// Bintray
Properties properties = new Properties()
properties.load(rootProject.file('local.properties').newDataInputStream())

group = properties.getProperty("bintray.group")
version = properties.getProperty("bintray.version")

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "komamj"
            name "komamj"
            email "junmaokoma@gmail.com"
        }
    }

    scm {
        url "https://github.com/komamj/componentization-architecture-gradle-plugin"
    }
}

publishing {
    publications {
        pluginMavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            groupId "com.komamj"
            artifactId "configuration-gradle-plugin"
            version "0.0.1"

            pom.withXml {
                def root = asNode()
                root.appendNode("description", "A common configuration gradle plugin for android componentization architecture.")
                root.appendNode("name", "configuration")
                root.appendNode("url", "https://github.com/komamj/componentization-architecture-gradle-plugin")
                root.children().last() + pomConfig
            }
        }
    }

    repositories {
        maven {
            url = "../repos"
        }
    }
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    publications = ['pluginMavenPublication']

    configurations = ['archives']

    pkg {
        repo = "maven"
        name = "configuration-gradle-plugin"
        userOrg = properties.getProperty("bintray.user")
        licenses = ['Apache-2.0']
        vcsUrl = "https://github.com/komamj/componentization-architecture-gradle-plugin.git"
        publish = true
        publicDownloadNumbers = true
        version {
            name = properties.getProperty("bintray.version")
            released = new Date()
            gpg {
                sign = true
            }
        }
    }
}